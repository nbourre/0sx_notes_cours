# D√©couverte du protocole I2C avec Arduino <!-- omit in toc -->

# Table des mati√®res <!-- omit in toc -->
- [Introduction](#introduction)
- [Qu'est-ce que le protocole I2C ?](#quest-ce-que-le-protocole-i2c-)
- [Fonctionnement de base d'I2C](#fonctionnement-de-base-di2c)
- [Utilit√© d'I2C dans le contexte Arduino](#utilit√©-di2c-dans-le-contexte-arduino)
- [Fonctionnement en programmation](#fonctionnement-en-programmation)
- [Exemple : Lecture d'un acc√©l√©rom√®tre MPU-6050](#exemple--lecture-dun-acc√©l√©rom√®tre-mpu-6050)
- [Exemple : Contr√¥le d'√©cran LCD 1602 I2C](#exemple--contr√¥le-d√©cran-lcd-1602-i2c)
- [Exemple : MPU-6050 avec la librairie Adafruit](#exemple--mpu-6050-avec-la-librairie-adafruit)
- [Exemples d'appareils communs utilisant I2C](#exemples-dappareils-communs-utilisant-i2c)
- [Conclusion](#conclusion)
- [Exercices](#exercices)
- [R√©f√©rences](#r√©f√©rences)



# Introduction
Lorsqu'on travaille avec des projets Arduino, il est fr√©quent de rencontrer des capteurs et des p√©riph√©riques qui communiquent via le protocole I2C. Cette m√©thode de communication est particuli√®rement int√©ressante pour les √©tudiants, car elle permet de simplifier les connexions entre les dispositifs et de gagner en flexibilit√©. Dans cet article, nous allons explorer les bases du protocole I2C, son utilit√© et un exemple d'application.

# Qu'est-ce que le protocole I2C ?
I2C, abr√©viation de "Inter-Integrated Circuit", est un protocole de communication invent√© par Philips (aujourd'hui NXP) en 1982. Il est con√ßu pour √©tablir une communication bidirectionnelle entre plusieurs p√©riph√©riques √©lectroniques sur un m√™me circuit imprim√©. Il fonctionne en mode ma√Ætre-esclave, c'est-√†-dire qu'un seul ma√Ætre (g√©n√©ralement un microcontr√¥leur comme l'Arduino) contr√¥le plusieurs esclaves (comme des capteurs, des √©crans LCD, etc.).

# Fonctionnement de base d'I2C
Le protocole I2C fonctionne en mode **ma√Ætre-esclave**. Le ma√Ætre est le dispositif qui contr√¥le les autres p√©riph√©riques. Il peut envoyer des donn√©es √† un esclave, mais aussi recevoir des donn√©es de l'esclave. Les esclaves sont les p√©riph√©riques qui re√ßoivent des donn√©es du ma√Ætre et qui peuvent envoyer des donn√©es au ma√Ætre. Dans le cas d'un Arduino, le ma√Ætre est l'Arduino lui-m√™me et les esclaves sont les capteurs et les p√©riph√©riques qui communiquent avec l'Arduino.

Le protocole I2C utilise deux lignes de communication: **SDA** (*Serial Data*) et **SCL** (*Serial Clock*). SDA est responsable de la transmission des donn√©es, tandis que SCL synchronise les dispositifs en fournissant une horloge partag√©e. Tous les dispositifs esclaves sont connect√©s en parall√®le √† ces deux lignes, ce qui permet de r√©duire consid√©rablement le nombre de c√¢bles n√©cessaire.

![Alt text](assets/I2C-Communication-How-It-Works.webp)

Chaque esclave poss√®de une **adresse unique** (g√©n√©ralement de 7 bits) qui permet au ma√Ætre de communiquer avec un p√©riph√©rique sp√©cifique. Lorsqu'un ma√Ætre souhaite envoyer ou recevoir des donn√©es, il commence par diffuser l'adresse de l'esclave concern√© sur le bus I2C. Une fois que l'esclave reconna√Æt son adresse, il √©tablit une connexion et √©change des donn√©es avec le ma√Ætre.

Comme indiqu√©, chaque appareil poss√®de une adresse. Celle-ci est g√©n√©ralement fournie par le fabricant du p√©riph√©rique. Par exemple, le module LCD I2C poss√®de l'adresse 0x27. Pour trouver l'adresse d'un p√©riph√©rique, il suffit de lire la documentation fournie par le fabricant (RTFM! üòâ).

Certains appareils permettent de modifier l'adresse √† l'aide de configuration. Par exemple, en plus de l'adresse 0x27 le module LCD I2C peut √™tre configur√© pour avoir une adresse entre 0x20 √† 0x23. Pour modifier l'adresse, il suffit de faire des ponts de soudure sur les broches A0, A1 et A2 du module LCD I2C.

![Alt text](assets/pcf8574.webp)

> **Question :** Sachant que les adresses sont de 7 bits, combien d'adresses diff√©rentes sont possibles?<details><summary>R√©ponse</summary>
> 128 appareils</details>

# Utilit√© d'I2C dans le contexte Arduino
Le protocole I2C est tr√®s utile dans les projets Arduino pour plusieurs raisons :

1. Simplicit√© de c√¢blage : Comme I2C n'utilise que deux lignes de communication, cela simplifie grandement le c√¢blage et r√©duit l'encombrement sur la carte
2. Scalabilit√© : √âtant donn√© que plusieurs dispositifs peuvent √™tre connect√©s au m√™me bus I2C, il est facile d'ajouter de nouveaux p√©riph√©riques sans affecter la configuration existante.
3. Compatibilit√© : De nombreux capteurs et p√©riph√©riques couramment utilis√©s avec Arduino sont compatibles avec I2C, comme les capteurs de temp√©rature, les √©crans OLED, les acc√©l√©rom√®tres, etc.

# Fonctionnement en programmation
Le fonctionnement g√©n√©rale pour communiquer avec un appareil I2C est le suivant :
1. Initialiser la communication I2C avec la fonction `Wire.begin()`
2. Ex√©cuter la fonction `Wire.beginTransmission()` pour commencer la transmission. Cette fonction prend en param√®tre l'adresse du p√©riph√©rique.
3. √âcrire la commande √† envoyer au p√©riph√©rique avec la fonction `Wire.write()`.
   - Cette commande peut √™tre une adresse de registre, une commande sp√©cifique, etc. Cela d√©pend du p√©riph√©rique.
4. √âcrire la fin de la transmission avec la fonction `Wire.endTransmission()`.
   - Cette fonction permet de terminer la transmission et de lib√©rer le bus I2C pour d'autres p√©riph√©riques.
5. Demander au p√©riph√©rique de renvoyer des donn√©es avec la fonction `Wire.requestFrom()`.
   - Cette fonction prend en param√®tre l'adresse du p√©riph√©rique et le nombre d'octets √† recevoir.
6. Regarder s'il y a des donn√©es disponibles avec la fonction `Wire.available()`.
   - Cette fonction retourne le nombre d'octets disponibles dans le buffer de r√©ception.
7. Lire les donn√©es avec la fonction `Wire.read()`.
   - Cette fonction lit les donn√©es dans le buffer de r√©ception et les renvoie sous forme d'un octet.
   - Les donn√©es re√ßues d√©pendent du p√©riph√©rique.

On s'aper√ßoit que la communication i2c est plus complexe que la communication s√©rie. C'est pourquoi il est important de bien lire la documentation du p√©riph√©rique pour savoir comment communiquer avec lui.

# Exemple : Lecture d'un acc√©l√©rom√®tre MPU-6050

![Alt text](assets/mpu6050_bb.svg)

Prenons l'exemple d'un acc√©l√©rom√®tre MPU-6050. Pour lire les donn√©es de l'acc√©l√©rom√®tre √† l'aide d'un Arduino, voici les √©tapes √† suivre :
1. Branchez les broches SDA et SCL du capteur aux broches correspondantes de l'Arduino Mega soit 20 et 21. N'oubliez pas de connecter √©galement les broches d'alimentation (VCC et GND) du capteur aux broches correspondantes de l'Arduino.
2. Importez la biblioth√®que "Wire" pour g√©rer la communication I2C. Dans l'IDE Arduino, allez dans Croquis > Importer une biblioth√®que > Wire.
3. √âcrivez le code. Voici un exemple :

```cpp
#include <Wire.h>

const int MPU_ADDR = 0x68; // Adresse I2C du MPU-6050

void setup() {
  Serial.begin(9600);         // Initialise la communication s√©rie
  Wire.begin();               // Initialise la communication I2C
  Wire.beginTransmission(MPU_ADDR);
  Wire.write(0x6B);           // S√©lectionne le registre PWR_MGMT_1
  Wire.write(0);              // D√©finit la valeur du registre √† 0 (r√©veille le MPU-6050)
  Wire.endTransmission(true);
}

void loop() {
  Wire.beginTransmission(MPU_ADDR);
  Wire.write(0x3B);           // Pointe vers le registre de donn√©es de l'acc√©l√©rom√®tre
  Wire.endTransmission(false);
  Wire.requestFrom(MPU_ADDR, 14, true); // Demande 14 octets de donn√©es (acc√©l√©rom√®tre + gyroscope)

  // Wire.read() lit 1 octet de donn√©es
  // On lit un octet que l'on d√©cale √† gauche de 8 positions
  // Ensuite on combine avec un autre octet lu
  // La combinaison de deux octets donne un entier de 16 bits
  int16_t ax = Wire.read() << 8 | Wire.read();
  int16_t ay = Wire.read() << 8 | Wire.read();
  int16_t az = Wire.read() << 8 | Wire.read();
  int16_t gx = Wire.read() << 8 | Wire.read();
  int16_t gy = Wire.read() << 8 | Wire.read();
  int16_t gz = Wire.read() << 8 | Wire.read();

  Serial.print("Accel_X:"); Serial.print(ax)
  Serial.print(", Accel_Y:"); Serial.print(ay);
  Serial.print(", Accel_Z:"); Serial.print(az);
  Serial.print(", Gyro_X:");  Serial.print(gx);
  Serial.print(", Gyro_Y:");  Serial.print(gy);
  Serial.print(", Gyro_Z:");  Serial.println(gz);

  delay(25); // Attend quelques instants avant la prochaine lecture
}

```

Cet exemple poss√®de une certaine complexit√©, car il y a beaucoup de choses √† comprendre. En plus, il faut lire le manuel du capteur qui indique comment √©changer de l'information avec celui-ci.

Heureusement, plusieurs appareils poss√®dent une librairie pour faciliter leur exploitation.

> **Note :** Lorsque vous voyez les op√©rateurs `<<` et `>>` il s'agit de l'op√©rateur de d√©calage binaire. Il permet de d√©caler les bits d'un octet vers la gauche ou la droite. L'op√©rateur `|` permet de combiner deux octets en un seul. Il s'agit d'un "ou binaire". L'op√©rateur `&` permet de combiner deux octets en un seul. Il s'agit d'un "et binaire".

# Exemple : Contr√¥le d'√©cran LCD 1602 I2C
Voici un exemple de code pour contr√¥ler un √©cran LCD 1602 I2C avec l'Arduino Mega. L'√©cran LCD 1602 I2C est un √©cran LCD 16x2 avec un contr√¥leur I2C int√©gr√©. Il est donc tr√®s facile de le connecter √† l'Arduino Mega en utilisant seulement deux broches (SDA et SCL). Voici le code :

```cpp
#include <Wire.h>
#include <LCD_I2C.h>

// Cr√©ez un objet LCD_I2C avec l'adresse du module I2C et la taille de l'√©cran (16x2)
LCD_I2C lcd(0x27, 16, 2);

void setup() {
  // Initialise l'√©cran LCD
  lcd.init();

  // Active le r√©tro√©clairage
  lcd.backlight();

  // Positionne le curseur √† la premi√®re colonne (0) et la premi√®re ligne (0)
  lcd.setCursor(0, 0);

  // Affiche "Bonjour !" √† la position du curseur
  lcd.print("Bonjour !");
}

void loop() {
  // Ajouter le code ici
}

```

Pour r√©aliser cet exemple, il faut importer la librairie `LCD_I2C`.


![Alt text](assets/branchement_lcd_i2c_bb.png)

# Exemple : MPU-6050 avec la librairie Adafruit
Voici un exemple avec la librairie fournie par Adafruit. Cette librairie est tr√®s simple √† utiliser et permet de lire les donn√©es de l'acc√©l√©rom√®tre et du gyroscope. Voici le code :

```cpp
#include <Adafruit_MPU6050.h>
#include <Adafruit_Sensor.h>
#include <Wire.h>

Adafruit_MPU6050 mpu;

void setup(void) {
  Serial.begin(115200);

  // Tentative d'initialisation
  if (!mpu.begin()) {
    Serial.println("Echec de l'initialisation du MPU6050");
    while (1) {
      delay(10);
    }
  }

  // Configuration de l'acc√©l√©rom√®tre
  mpu.setAccelerometerRange(MPU6050_RANGE_16_G);
  mpu.setGyroRange(MPU6050_RANGE_250_DEG);
  mpu.setFilterBandwidth(MPU6050_BAND_21_HZ);
  Serial.println("Fin de la configuration");
  delay(100);
}

void loop() {

  // Obtention des donn√©es de l'acc√©l√©rom√®tre et du gyroscope
  sensors_event_t a, g, temp;

  // Utilisez les fonctions getEvent() pour obtenir les donn√©es de l'acc√©l√©rom√®tre et du gyroscope
  mpu.getEvent(&a, &g, &temp);

  // Afficher les valeurs
  Serial.print("AccelX:"); Serial.print(a.acceleration.x); Serial.print(",");
  Serial.print("AccelY:"); Serial.print(a.acceleration.y); Serial.print(",");
  Serial.print("AccelZ:"); Serial.print(a.acceleration.z); Serial.print(", "); 
  Serial.print("GyroX:"); Serial.print(g.gyro.x); Serial.print(",");
  Serial.print("GyroY:"); Serial.print(g.gyro.y); Serial.print(",");
  Serial.print("GyroZ:"); Serial.print(g.gyro.z); Serial.println("");

  delay(10);
}

```

Si l'on compare avec la version sans librairie, on remarque que le code est beaucoup plus simple √† comprendre et √† utiliser. Il suffit de lire la documentation de la librairie pour savoir comment utiliser les fonctions.

> **Note sur les pointeurs**
> 
> Vous avez probablement remarquer l'utilisation des caract√®res `&` devant les variable `a`, `g` et `temp`.
> 
> Ce caract√®re signifie "L'adresse de". Il permet de passer une adresse de variable √† une fonction. Cela permet de modifier la valeur d'une variable dans une fonction.
> 
> Par exemple, si on veut modifier la valeur de la variable `a` dans la fonction `getEvent()`, il faut passer l'adresse de la variable `a` √† la fonction. C'est ce que fait le `&a`. On pourrait lire comme ceci "passe l'adresse de `a`".
> 
> Si vous ne comprenez pas ce concept, vous pouvez consulter le tutoriel sur les pointeurs.

# Exemples d'appareils communs utilisant I2C
Voici quelques exemples d'appareils et de capteurs populaires qui fonctionnent avec le protocole I2C et sont souvent utilis√©s dans des projets Arduino :

1. **MPU-6050 :** Un capteur combinant un acc√©l√©rom√®tre et un gyroscope sur un seul module. Il est fr√©quemment utilis√© dans les projets de stabilisation, de contr√¥le de mouvement et de navigation.

![Alt text](assets/mpu6050.jpg)

2. **PCA9685 :** Un pilote de servo-moteur et de LED √† 16 canaux permettant de contr√¥ler jusqu'√† 16 servo-moteurs ou LEDs ind√©pendamment. Il est utilis√© dans les projets de robotique et d'√©clairage.
![Alt text](assets/pca9685.jpg)

1. **DS1307 :** Une horloge temps r√©el (RTC) qui maintient l'heure et la date m√™me lorsque l'Arduino est √©teint. Elle est souvent employ√©e dans les projets n√©cessitant une gestion pr√©cise du temps, comme les horloges ou les enregistreurs de donn√©es.
![Alt text](assets/ds1307.jpg)

1. **PCF8574 :** Un contr√¥leur de port I/O √† 8 broches qui permet de contr√¥ler des p√©riph√©riques externes √† l'aide d'un bus I2C. Il est utilis√© dans les projets n√©cessitant un contr√¥le de p√©riph√©riques √† distance, comme les claviers matriciels ou les afficheurs LCD.
   - Dans votre cas, il est pr√©soud√© sur le module LCD.
![Alt text](assets/pcf8574.webp)

1. **SSD1306 :** Un contr√¥leur pour √©crans OLED monochromes couramment utilis√© avec des √©crans de petite taille (par exemple, 128x64 ou 128x32 pixels). Ces √©crans sont souvent employ√©s pour afficher des informations sur l'√©tat, des graphiques ou des textes dans divers projets.
![Alt text](assets/ssd1306.JPG)

1. **BMP280 :** Un capteur de pression atmosph√©rique et de temp√©rature de Bosch Sensortec. Il est couramment utilis√© dans les projets m√©t√©orologiques et d'altim√©trie.
![Alt text](assets/bmp280.webp)

# Conclusion
Le protocole I2C est un moyen simple et efficace de communiquer avec divers p√©riph√©riques dans les projets Arduino. Il permet de simplifier le c√¢blage et d'interagir facilement avec de nombreux capteurs et p√©riph√©riques. En comprenant les bases d'I2C et en ma√Ætrisant son utilisation, vous pourrez cr√©er des projets plus complexes et plus performants.

---

# Exercices
1. √Ä l'aide de votre kit, ajoutez √† votre montage le module LCD i2c et affichez le texte "Hello World" sur l'√©cran LCD.
2. Ajoutez l'ensemble des appareils i2c qui sont pr√©sent dans votre kit et affichez les valeurs que vous lisez sur l'√©cran LCD.

---

# R√©f√©rences
- [& - Arduino Reference](https://www.arduino.cc/reference/en/language/structure/pointer-access-operators/reference/)