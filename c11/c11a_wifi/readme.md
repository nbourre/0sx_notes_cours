# D√©but Wifi <!-- omit in toc -->

![alt text](<assets/DALL¬∑E 2024-03-21 08.57.44.webp>)

# Table des mati√®res <!-- omit in toc -->
- [Introduction](#introduction)
- [ESP8266](#esp8266)
  - [Les commandes AT](#les-commandes-at)
- [Le ESP8266 et le shield ESP8266](#le-esp8266-et-le-shield-esp8266)
  - [Le ESP-01](#le-esp-01)
    - [Branchement](#branchement)
    - [Code pour configurer le wifi](#code-pour-configurer-le-wifi)
    - [Initialisation du module](#initialisation-du-module)
      - [Fonctions d'aide](#fonctions-daide)
    - [Exemple de code pour le ESP-01](#exemple-de-code-pour-le-esp-01)
  - [Le shield ESP8266](#le-shield-esp8266)
    - [Branchement](#branchement-1)
    - [Requis - IMPORTANT!!](#requis---important)
    - [Code pour configurer le wifi](#code-pour-configurer-le-wifi-1)
- [Exemples pour communiquer avec le module ESP8266](#exemples-pour-communiquer-avec-le-module-esp8266)
  - [Exemple de code - Serveur Web](#exemple-de-code---serveur-web)
  - [Exemple - Allumer une DEL](#exemple---allumer-une-del)
- [R√©sum√©](#r√©sum√©)
- [R√©f√©rences](#r√©f√©rences)


# Introduction
Nous avons vu les notions de lecture pour le port s√©rie dans le cours suivant : [Lecture de donn√©es √† partir du port s√©rie](../../c08/c08a_serial_read/readme.md).

Dans ce cours, nous allons voir comment communiquer avec un Arduino Mega via le port s√©rie en utilisant un shield qui poss√®de un microontr√¥leur ESP8266.

![Alt text](assets/esp8266-wifi-shield-desc2.jpg)

# ESP8266
Avant de d√©buter avec le shield, nous devons en conna√Ætre un peu plus sur l'ESP8266.

L'ESP8266 est un microcontr√¥leur qui peut √™tre programm√© en langage Arduino. Si vous √™tes un nouveau programmeur qui commence √† programmer en C, vous pourriez √™tre int√©ress√© par cette petite puce car elle est facile √† utiliser et offre une grande vari√©t√© de fonctionnalit√©s.

![Diff√©rents models d'ESP](assets/ModelosESP.avif)

Cette image montre la vari√©t√© de mod√®les d'ESP disponibles sur le march√©. Vous pouvez trouver des mod√®les avec des caract√©ristiques diff√©rentes, mais tous sont bas√©s sur le m√™me microcontr√¥leur ESP8266.

L'ESP8266 est un module peu co√ªteux d'o√π sa popularit√©. Il est √©galement tr√®s facile √† utiliser et √† programmer. Il est √©galement puissant et peut √™tre utilis√© pour cr√©er des projets complexes.

L'ESP8266 est principalement utilis√© pour se connecter √† Internet et interagir avec des services en ligne. Vous pouvez cr√©er des projets tels que des capteurs connect√©s, des syst√®mes d'automatisation domestique ou m√™me des robots qui communiquent avec un serveur distant. Il peut √©galement √™tre utilis√© pour contr√¥ler des appareils √©lectroniques tels que des lumi√®res, des ventilateurs et des moteurs.

D'ailleurs, il est utilis√© dans plusieurs produits commerciaux, tels que les cam√©ras de surveillance, les thermostats intelligents, les syst√®mes de s√©curit√© et les syst√®mes de contr√¥le d'acc√®s. L'un des gros acteurs dans l'industrie est Tuya, qui utilise l'ESP8266 pour cr√©er des appareils connect√©s. D'ailleurs si vous avez des appareils intelligents √©conomiques √† la maison, il y a de forte chance que Tuya soit derri√®re. Pour la note, Tuya est une entreprise chinoise.

L'ESP8266 est √©quip√© d'un processeur 32 bits √† faible consommation d'√©nergie et dispose de 80 √† 160 Ko de m√©moire flash pour le stockage de programmes. Il prend en charge le Wi-Fi et peut √™tre connect√© √† un r√©seau local ou √† un point d'acc√®s Wi-Fi pour se connecter √† Internet. Il est √©galement compatible avec de nombreuses biblioth√®ques et outils de d√©veloppement, ce qui facilite la cr√©ation de projets complexes.

Pour commencer √† programmer l'ESP8266, vous devez disposer d'un module ESP8266 et d'un c√¢ble USB pour le connecter √† votre ordinateur. Vous devez √©galement t√©l√©charger et installer l'environnement Arduino. Une fois cela fait, vous pouvez ouvrir l'interface Arduino, s√©lectionner votre carte ESP8266 et commencer √† √©crire votre code.

En r√©sum√©, l'ESP8266 est un microcontr√¥leur puissant et facile √† utiliser qui peut √™tre programm√©, entre autres, en langage C/C++. Il est √©quip√© d'un processeur 32 bits, de Wi-Fi et d'une grande quantit√© de m√©moire flash pour stocker des programmes. En utilisant l'environnement Arduino, vous pouvez facilement commencer √† programmer l'ESP8266 et cr√©er des projets connect√©s √† Internet passionnants.

---

## Les commandes AT
Si le module ESP8266 est programm√© avec le **firmware AT**, il peut √™tre contr√¥l√© par des commandes AT. Ainsi, il ne sera pas n√©cessaire de programmer directement le module ESP8266 pour l'utiliser. Il suffit de lui envoyer des commandes AT via le port s√©rie.

Les commandes AT sont des commandes que le module ESP8266, avec le bon firmware, comprend. Ces commandes sont envoy√©es au module ESP8266 via le port s√©rie. Le module ESP8266 r√©pondra √† ces commandes avec des donn√©es ou des informations.

Elles permettent, entre autres, de configurer les informations de connexion.

Dans notre cas, nous utilisons une librairie qui cachera ces commandes AT. Ce qui nous permettra de nous concentrer sur le code de notre application.

> **Note :** Le mot **firmware** d√©signe le programme qui est charg√© sur un microcontr√¥leur. Il est diff√©rent du programme qui est charg√© sur un ordinateur. Le firmware est √©crit en langage assembleur ou en langage C/C++. En fait, lorsque vous programmez votre Arduino, vous √™tes en train de programmer le firmware de votre microcontr√¥leur.

---

# Le ESP8266 et le shield ESP8266
Dans le cours, il y a deux mod√®les pour le module ESP8266. Le premier est le module ESP-01 et le deuxi√®me est le shield ESP8266. Pour le cours, le code est identique pour les deux modules.

üëâ **ATTENTION!! Lisez la documentation qui s'applique √† votre module.** üëà

## Le ESP-01
Documentation pour ce mod√®le

![alt text](assets/esp01_shield.jpg)

Le module ESP-01 est un module Wi-Fi bas√© sur le SoC ESP8266. Il est √©quip√© d'une antenne PCB int√©gr√©e et d'un connecteur de 2x4 broches. Il est con√ßu pour √™tre utilis√© dans des applications IoT (Internet des objets).



Celui utilis√© en classe vient aussi avec un shield qui expose les broches RX et TX pour la communication s√©rie. Ainsi, il est possible de le brancher directement sur un Arduino.

### Branchement
Le branchement est simple. Il suffit de brancher la broche RX du module ESP-01 sur la broche TX3 du Mega et la broche TX du module ESP-01 sur la broche RX3 du Mega.

![alt text](assets/branchement_rxtx.jpg)

### Code pour configurer le wifi
Pour utiliser le ESP-01, il faudra utiliser la librairie "WifiEsp.h" qui permet de communiquer avec le module.

### Initialisation du module
```cpp

#include <WiFiEsp.h>

// Mettre √† 1 si le fichier arduino_secrets.h est pr√©sent
#define HAS_SECRETS 0

#if HAS_SECRETS
#include "arduino_secrets.h"
/////// SVP par soucis de s√©curit√©, mettez vos informations dans le fichier arduino_secrets.h

// Nom et mot de passe du r√©seau wifi
const char ssid[] = SECRET_SSID;
const char pass[] = SECRET_PASS;

#else
const char ssid[] = "TechniquesInformatique-Etudiant";  // your network SSID (name)
const char pass[] = "shawi123";                         // your network password (use for WPA, or use as key for WEP)
#endif

#define AT_BAUD_RATE 115200

int blinkRate = 500;

int status = WL_IDLE_STATUS;     // Status du module wifi

void setup() {
  pinMode(LED_BUILTIN, OUTPUT);

  Serial.begin(AT_BAUD_RATE);
  while (!Serial)
    ;

  
  // D√©marre la communication s√©rie avec le module ESP (connect√© √† Serial1)
  Serial1.begin(AT_BAUD_RATE);

  // Initialise la biblioth√®que WiFiEspAT avec ce port s√©rie
  WiFi.init(&Serial1);           

  // Cela peut prendre un certain temps pour que le module wifi soit pr√™t
  // Voir 1 minute dans la documentation
  if (WiFi.status() == WL_NO_SHIELD) {
    Serial.println();
    Serial.println("La communication avec le module WiFi a √©chou√©!");

    // Clignote 2.1 pour indiquer cette erreur
    errorState(2, 1);
  }

  Serial.print("Attempting to connect to WPA SSID: ");
  Serial.println(ssid);

  // Tentative de connexion au Wifi
  while (status != WL_CONNECTED) {
    Serial.print(".");

    // Connecter au ssid
    status = WiFi.begin(ssid, pass);
  }

  Serial.println("Vous √™tes connect√© au r√©seau");
  printWifiStatus();
  Serial.println();


}

void loop() {
  static unsigned long lastBlink = 0;
  if (millis() - lastBlink >= blinkRate) {
    lastBlink = millis();
    digitalWrite(LED_BUILTIN, !digitalRead(LED_BUILTIN));
  }
}
```

#### Fonctions d'aide
Code pour les fonctions suppl√©mentaires `printWifiStatus` et `errorState`.

<details><summary>Ouvrir pour voir le code</summary>

```cpp
void printWifiStatus() {

  // imprime le SSID du r√©seau auquel vous √™tes connect√©:
  char ssid[33];
  WiFi.SSID(ssid);
  Serial.print("SSID: ");
  Serial.println(ssid);

  // imprime le BSSID du r√©seau auquel vous √™tes connect√©:
  uint8_t bssid[6];
  WiFi.BSSID(bssid);
  Serial.print("BSSID: ");
  printMacAddress(bssid);

  uint8_t mac[6];
  WiFi.macAddress(mac);
  Serial.print("MAC: ");
  printMacAddress(mac);

  // imprime l'adresse IP de votre carte:
  IPAddress ip = WiFi.localIP();
  Serial.print("Adresse IP: ");
  Serial.println(ip);

  // imprime la force du signal re√ßu:
  long rssi = WiFi.RSSI();
  Serial.print("force du signal (RSSI):");
  Serial.print(rssi);
  Serial.println(" dBm");
}

// Imprime l'adresse MAC
void printMacAddress(byte mac[]) {
  for (int i = 5; i >= 0; i--) {
    if (mac[i] < 16) {
      Serial.print("0");
    }
    Serial.print(mac[i], HEX);
    if (i > 0) {
      Serial.print(":");
    }
  }
  Serial.println();
}

// Fonction affichant un √©tat d'erreur avec 2 param√®tres pour code d'erreur
// Cela permet de clignoter la DEL avec un code pour faciliter le d√©bogage pour
// l'utilisateur. On ne sort jamais de cette fonction.
// Le programmeur doit d√©finir la signification des codes d'erreur.
//
// Exemple de code d'erreur:
//   errorState (2, 3) <-- (clignote 2 fois, pause, clignote 3 fois)
void errorState(int codeA, int codeB) {
  const int rate = 100;
  const int pauseBetween = 500;
  const int pauseAfter = 1000;

  // On ne sort jamais de cette fonction
  while (true) {
    for (int i = 0; i < codeA; i++) {
      digitalWrite(LED_BUILTIN, HIGH);
      delay(rate);
      digitalWrite(LED_BUILTIN, LOW);
      delay(rate);
    }
    delay(pauseBetween);
    for (int i = 0; i < codeB; i++) {
      digitalWrite(LED_BUILTIN, HIGH);
      delay(rate);
      digitalWrite(LED_BUILTIN, LOW);
      delay(rate);
    }
    delay(pauseAfter);

    Serial.print("Erreur : ");
    Serial.print(codeA);
    Serial.print(".");
    Serial.println(codeB);
  }
}
```
</details>

---

### Exemple de code pour le ESP-01

```cpp
/*
 Exemple WiFiEsp : Serveur Web

 Un serveur Web simple qui affiche la valeur des entr√©es analogiques
 via une page web en utilisant un module ESP8266.
 Ce sketch affichera l'adresse IP de votre module ESP8266 (une fois connect√©)
 dans le moniteur s√©rie. Vous pourrez ensuite ouvrir cette adresse dans un navigateur
 pour afficher la page web.
 La page web sera automatiquement actualis√©e toutes les 20 secondes.

 Pour plus de d√©tails, voir : http://yaab-arduino.blogspot.com/p/wifiesp.html
*/

#include "WiFiEsp.h"

#define AT_BAUD_RATE 115200

const char ssid[] = "TechniquesInformatique-Etudiant";  // SSID (nom) de votre r√©seau
const char pass[] = "shawi123";                         // Mot de passe de votre r√©seau (WPA ou cl√© WEP)
int status = WL_IDLE_STATUS;     // √âtat de la connexion WiFi
int reqCount = 0;                // Nombre de requ√™tes re√ßues

WiFiEspServer server(80);

void setup()
{
  // Initialiser la liaison s√©rie pour le d√©bogage
  Serial.begin(AT_BAUD_RATE);
  // Initialiser la liaison s√©rie pour le module ESP
  Serial1.begin(AT_BAUD_RATE);
  // Initialiser le module ESP
  WiFi.init(&Serial1);

  // V√©rifier la pr√©sence du shield
  if (WiFi.status() == WL_NO_SHIELD) {
    Serial.println("Module WiFi non d√©tect√©");
    while (true);
  }

  // Tentative de connexion au r√©seau WiFi
  while (status != WL_CONNECTED) {
    Serial.print("Tentative de connexion au r√©seau WPA : ");
    Serial.println(ssid);
    status = WiFi.begin(ssid, pass);
  }

  Serial.println("Connect√© au r√©seau");
  printWifiStatus();

  // D√©marrer le serveur web sur le port 80
  server.begin();
}

void loop()
{
  // √âcouter les clients entrants
  WiFiEspClient client = server.available();
  if (client) {
    Serial.println("Nouveau client");
    bool currentLineIsBlank = true;
    while (client.connected()) {
      if (client.available()) {
        char c = client.read();
        Serial.write(c);

        if (c == '\n' && currentLineIsBlank) {
          Serial.println("Envoi de la r√©ponse");

          client.print(
            "HTTP/1.1 200 OK\r\n"
            "Content-Type: text/html\r\n"
            "Connection: close\r\n"
            "Refresh: 20\r\n"
            "\r\n");
          client.print("<!DOCTYPE HTML>\r\n");
          client.print("<html>\r\n");
          client.println("<head>");
          // actualisez la page automatiquement toutes les 5 secondes
          client.println("<meta http-equiv=\"refresh\" content=\"5\" charset=\"UTF-8\">");
          client.println("</head>");
          client.print("<h1>Bonjour le monde !</h1>\r\n");
          client.print("Requ√™tes re√ßues : ");
          client.print(++reqCount);
          client.print("<br>\r\n");
          
          // afficher la valeur des broches d'entr√©e analogique
          for (int analogChannel = 0; analogChannel < 4; analogChannel++) {
            int sensorReading = analogRead(analogChannel);
            client.print("Entree analogique ");
            client.print(analogChannel);
            client.print(" est ");
            client.print(sensorReading);
            client.println("<br />");
          }
          
          client.print("</html>\r\n");
          break;
        }
        if (c == '\n') {
          currentLineIsBlank = true;
        }
        else if (c != '\r') {
          currentLineIsBlank = false;
        }
      }
    }
    delay(10);
    client.stop();
    Serial.println("Client d√©connect√©");
  }
}

void printWifiStatus()
{
  Serial.print("SSID : ");
  Serial.println(WiFi.SSID());

  IPAddress ip = WiFi.localIP();
  Serial.print("Adresse IP : ");
  Serial.println(ip);

  Serial.println();
  Serial.print("Pour voir cette page en action, ouvrez un navigateur √† l'adresse http://");
  Serial.println(ip);
  Serial.println();
}

```


---

## Le shield ESP8266

Documentation pour ce mod√®le

![alt text](assets/esp8266-wifi-shield-desc2.jpg)

Le module ESP8266 WiFi Shield est un module UART-WiFi. Le module est sp√©cialement con√ßu pour les appareils mobiles et l'Internet des objets (IoT).

Ce Shield WiFi est bas√© sur ESP-12F. Avec ce Shield, vous pouvez facilement connecter votre Arduino au r√©seau et contr√¥ler votre appareil de n'importe o√π. La communication se fait via une interface UART et le contr√¥le se fait par commande AT.

- Supporte la norme sans fil 802.11 b/g/n
- Supporte les trois modes de travail STA/AP/STA + AP
- Pile de protocoles TCP/IP int√©gr√©e, et supporte plusieurs connexions TCP Client
- Supporte une riche s√©rie de commandes AT pour Socket

### Branchement
La documentation en ligne montre un branchement du shield sur un Arduino Uno. Pour le branchement sur un Arduino Mega, il faut d√©sactiver les cavaliers comme montr√© sur l'image ci-dessous.

Ensuite, il faut brancher une des broches ESP_RX sur la broche TX1 du Mega et la broche ESP_TX sur la broche RX1 du Mega.

![Branchement](https://raw.githubusercontent.com/nbourre/documentations_generiques/cb843f0d7438001efba129667d5b35b14bcb41a5/embedded/esp8266/makerfabs_wifi_mega.jpg)

Dans le code, il faudra alors utiliser les fonctions `Serial1` pour √©changer avec le module.

### Requis - IMPORTANT!!
Pour le cours, il faut installer la librairie **WiFiEspAT** pour pouvoir utiliser le shield.

### Code pour configurer le wifi
Le code suivant permet de configurer le module wifi pour se connecter √† un r√©seau wifi de mani√®re persistante. Il faut donc le faire une seule fois ou √† chaque fois que l'on d√©sire de changer de r√©seau wifi.

```cpp
#include <WiFiEspAT.h>

// Mettre √† 1 si le fichier arduino_secrets.h est pr√©sent
#define HAS_SECRETS 0  

#if HAS_SECRETS
#include "arduino_secrets.h"
/////// SVP par soucis de s√©curit√©, mettez vos informations dans le fichier arduino_secrets.h

// Nom et mot de passe du r√©seau wifi
const char ssid[] = SECRET_SSID;
const char pass[] = SECRET_PASS;

#else
const char ssid[] = "TechniquesInformatique-Etudiant";  // your network SSID (name)
const char pass[] = "shawi123";                         // your network password (use for WPA, or use as key for WEP)

#endif

#define AT_BAUD_RATE 115200

int blinkRate = 500;

void setup() {
  pinMode(LED_BUILTIN, OUTPUT);

  Serial.begin(115200);
  while (!Serial);

  Serial1.begin(AT_BAUD_RATE);
  WiFi.init(&Serial1);

  // Cela peut prendre un certain temps pour que le module wifi soit pr√™t
  // Voir 1 minute dans la documentation
  if (WiFi.status() == WL_NO_MODULE) {
    Serial.println();
    Serial.println("La communication avec le module WiFi a √©chou√©!");
    // ne pas continuer
    errorState(2, 1);
  }

  WiFi.disconnect();  // pour effacer le chemin. non persistant

  WiFi.setPersistent();  // d√©finir la connexion WiFi suivante comme persistante

  WiFi.endAP();  // pour d√©sactiver le d√©marrage automatique persistant AP par d√©faut au d√©marrage

  // d√©commentez ces lignes pour une adresse IP statique persistante. d√©finissez des adresses valides pour votre r√©seau
  // Pour l'adresse du c√©gep, la plage est 172.22.0.0 o√π les derniers octets sont entre 1 et 254
  // IPAddress ip(192, 168, 1, 9);
  // IPAddress gw(192, 168, 1, 1);
  // IPAddress nm(255, 255, 255, 0);
  // WiFi.config(ip, gw, gw, nm);

  Serial.println();
  Serial.print("Tentative de connexion √† SSID: ");
  Serial.println(ssid);

  int status = WiFi.begin(ssid, pass);

  if (status == WL_CONNECTED) {
    Serial.println();
    Serial.println("Connect√© au r√©seau WiFi.");
    printWifiStatus();
  } else {
    WiFi.disconnect();  // supprimer la connexion WiFi
    Serial.println();
    Serial.println("La connexion au r√©seau WiFi a √©chou√©.");
    blinkRate = 100;
  }
}

void loop() {
  static unsigned long lastBlink = 0;
  if (millis() - lastBlink >= blinkRate) {
    lastBlink = millis();
    digitalWrite(LED_BUILTIN, !digitalRead(LED_BUILTIN));
  }
}

void printWifiStatus() {

  // imprime le SSID du r√©seau auquel vous √™tes connect√©:
  char ssid[33];
  WiFi.SSID(ssid);
  Serial.print("SSID: ");
  Serial.println(ssid);

  // imprime le BSSID du r√©seau auquel vous √™tes connect√©:
  uint8_t bssid[6];
  WiFi.BSSID(bssid);
  Serial.print("BSSID: ");
  printMacAddress(bssid);

  uint8_t mac[6];
  WiFi.macAddress(mac);
  Serial.print("MAC: ");
  printMacAddress(mac);

  // imprime l'adresse IP de votre carte:
  IPAddress ip = WiFi.localIP();
  Serial.print("Adresse IP: ");
  Serial.println(ip);

  // imprime la force du signal re√ßu:
  long rssi = WiFi.RSSI();
  Serial.print("force du signal (RSSI):");
  Serial.print(rssi);
  Serial.println(" dBm");
}

// Imprime l'adresse MAC
void printMacAddress(byte mac[]) {
  for (int i = 5; i >= 0; i--) {
    if (mac[i] < 16) {
      Serial.print("0");
    }
    Serial.print(mac[i], HEX);
    if (i > 0) {
      Serial.print(":");
    }
  }
  Serial.println();
}

// Fonction affichant un √©tat d'erreur avec 2 param√®tres pour code d'erreur
// Cela permet de clignoter la DEL avec un code pour faciliter le d√©bogage pour
// l'utilisateur. On ne sort jamais de cette fonction.
// Le programmeur doit d√©finir la signification des codes d'erreur.
//
// Exemple de code d'erreur:
//   errorState (2, 3) <-- (clignote 2 fois, pause, clignote 3 fois)
void errorState(int codeA, int codeB) {
  const int rate = 100;
  const int pauseBetween = 500;
  const int pauseAfter = 1000;

  // On ne sort jamais de cette fonction
  while (true) {
    for (int i = 0; i < codeA; i++) {
      digitalWrite(LED_BUILTIN, HIGH);
      delay(rate);
      digitalWrite(LED_BUILTIN, LOW);
      delay(rate);
    }
    delay(pauseBetween);
    for (int i = 0; i < codeB; i++) {
      digitalWrite(LED_BUILTIN, HIGH);
      delay(rate);
      digitalWrite(LED_BUILTIN, LOW);
      delay(rate);
    }
    delay(pauseAfter);

    Serial.print("Erreur : ");
    Serial.print(codeA);
    Serial.print(".");
    Serial.println(codeB);
  }
}


```

---

# Exemples pour communiquer avec le module ESP8266
Une fois la configuration faite, il est possible de communiquer avec le module ESP8266. Il y a plusieurs exemples disponibles dans la librairie `WiFiEspAT`. Voici deux exemples qui montrent comment utiliser le module ESP8266 pour cr√©er un serveur web.
Le premier exemple est un serveur web qui affiche la valeur des broches d'entr√©e analogique. Le deuxi√®me exemple est un serveur web qui permet d'allumer ou d'√©teindre une DEL en fonction de ce qui a √©t√© re√ßu par le module Wifi.

## Exemple de code - Serveur Web

Ce code est un exemple d'utilisation de la biblioth√®que `WiFiEspAT` pour cr√©er un serveur web √† l'aide d'un module Wifi ESP8266 branch√© sur le port s√©rie.

**Important :** Il faudra ex√©cuter l'exemple du code pour configurer le module wifi pour se connecter √† un r√©seau wifi de mani√®re persistante avant de pouvoir utiliser ce code.

```cpp
/*
Serveur Web WiFi

Un simple serveur web qui affiche la valeur des broches d'entr√©e analogique.

Cr√©√© le 13 Juillet 2010
par dlf (Metodo2 srl)
modifi√© le 31 mai 2012
par Tom Igoe
modifi√© en juillet 2019 pour la biblioth√®que WiFiEspAT
par Juraj Andrassy https://github.com/jandrassy
*/

#include <WiFiEspAT.h>

#define AT_BAUD_RATE 115200

WiFiServer server(80);

void setup() {

  Serial.begin(115200);
  while (!Serial);

  Serial1.begin(AT_BAUD_RATE);
  WiFi.init(Serial1);

  if (WiFi.status() == WL_NO_MODULE) {
    Serial.println("La communication avec le module WiFi a √©chou√© !");
    // ne pas continuer et aller dans une boucle d'erreur
    errorState();
  }

  // En attendant la connexion au r√©seau Wifi configur√© avec le sketch SetupWiFiConnection
  Serial.println("En attente de connexion au WiFi");
  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.print('.');
  }
  Serial.println();

  server.begin();

  IPAddress ip = WiFi.localIP();
  Serial.println();
  Serial.println("Connect√© au r√©seau WiFi.");
  Serial.print("Pour acc√©der au serveur, entrez \"http://");
  Serial.print(ip);
  Serial.println("/\" dans un navigateur web.");
}

void loop() {

  WiFiClient client = server.available();
  if (client) {
    IPAddress ip = client.remoteIP();
    Serial.print("nouveau client ");
    Serial.println(ip);

    while (client.connected()) {
      if (client.available()) {
        String line = client.readStringUntil('\n');
        line.trim();
        Serial.println(line);

        // si vous avez atteint la fin de l'en-t√™te HTTP (la ligne est vide),
        // la demande HTTP est termin√©e, vous pouvez donc envoyer une r√©ponse
        if (line.length() == 0) {
          // Ligne de statut HTTP
          client.println("HTTP/1.1 200 OK");

          // envoyer un en-t√™te de r√©ponse HTTP
          client.println("Content-Type: text/html");
          client.println();

          // Corps du HTML
          client.println("<!DOCTYPE HTML>");
          client.println("<html>");
          client.println("<head>");
          // actualisez la page automatiquement toutes les 5 secondes
          client.println("<meta http-equiv=\"refresh\" content=\"5\" charset=\"UTF-8\">");
          client.println("</head>");

          // afficher la valeur des broches d'entr√©e analogique
          for (int analogChannel = 0; analogChannel < 4; analogChannel++) {
            int sensorReading = analogRead(analogChannel);
            client.print("Entree analogique ");
            client.print(analogChannel);
            client.print(" est ");
            client.print(sensorReading);
            client.println("<br />");
          }
          
          client.println("</html>");
          client.flush();
          break;
        }
      }
    }

    // fermer la connexion:
    client.stop();
    Serial.println("client d√©connect√©");
  }
}

void errorState() {
  while (true) {
    digitalWrite(LED_BUILTIN, HIGH);
    delay(100);
    digitalWrite(LED_BUILTIN, LOW);
    delay(100);
  }
}

```

## Exemple - Allumer une DEL

Voici un exemple dans lequel on allume ou √©teint une DEL en fonction de ce qui a √©t√© re√ßu par le module Wifi.

**Important :** Il faudra ex√©cuter l'exemple du code pour configurer le module wifi pour se connecter √† un r√©seau wifi de mani√®re persistante avant de pouvoir utiliser ce code.

**Important 2 :** On doit avoir la classe `RingBuffer`. Dans mon [dossier des projets](https://github.com/nbourre/0sx_projets_cours), elle disponible dans le dossier [`appareils/wifi/wifi_webServer_led`](https://github.com/nbourre/0sx_projets_cours/tree/main/appareils/wifi/wifi_webServer_led).

```cpp
#include <WiFiEspAT.h>
#include "RingBuffer.h"

#define AT_BAUD_RATE 115200

WiFiServer server(80);

// Utilise un tampon circulaire pour am√©liorer
// la vitesse et r√©duire l'allocation de m√©moire
RingBuffer buf(8);

int ledStatus = 0;

void setup() {
  pinMode(LED_BUILTIN, OUTPUT);

  Serial.begin(115200);
  while (!Serial)
    ;

  Serial1.begin(AT_BAUD_RATE);
  WiFi.init(Serial1);

  if (WiFi.status() == WL_NO_MODULE) {
    Serial.println("La communication avec le module WiFi a √©chou√© !");
    // ne pas continuer
    while (true)
      ;
  }

  // En attendant la connexion au r√©seau Wifi configur√© avec le sketch SetupWiFiConnection
  Serial.println("En attente de connexion au WiFi");
  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.print('.');
  }
  Serial.println();

  server.begin();

  IPAddress ip = WiFi.localIP();
  Serial.println();
  Serial.println("Connect√© au r√©seau WiFi.");
  Serial.print("Pour acc√©der au serveur, entrez \"http://");
  Serial.print(ip);
  Serial.println("/\" dans un navigateur web.");
}

void loop() {
  wifiTask();
}

void wifiTask() {
    WiFiClient client = server.available();

  // Si vous obtenez un client
  if (client) {
    IPAddress ip = client.remoteIP();
    Serial.print("nouveau client ");
    Serial.println(ip);

    // Initialiser le tampon circulaire
    buf.init();

    // Boucler tant que le client est connect√©
    while (client.connected()) {

      // Si des octets sont disponibles √† partir du client
      if (client.available()) {

        char c = client.read();  // lire un octet et
        buf.push(c);             // le pousser dans le tampon circulaire

        // √âcrire dans le flux s√©rie va ralentir le code
        //Serial.write(c);

        // V√©rifiez si la requ√™te HTTP est termin√©e en d√©tectant deux caract√®res
        // de nouvelle ligne cons√©cutifs "\r\n\r\n", puis envoyez une r√©ponse
        if (buf.endsWith("\r\n\r\n")) {
          sendHttpResponse(client);
          break;
        }

        // Regarder si le client a demand√© √©tait "GET /H" or "GET /L":
        if (buf.endsWith("GET /H")) {
          Serial.println("Turn led ON");
          digitalWrite(LED_BUILTIN, HIGH);
          ledStatus = 1;
        } else if (buf.endsWith("GET /L")) {
          Serial.println("Turn led OFF");
          digitalWrite(LED_BUILTIN, LOW);

          ledStatus = 0;
        }
      }
    }

    // fermer la connexion:
    client.stop();
    Serial.println("client d√©connect√©");
  }
}

void sendHttpResponse(WiFiClient client) {
  sendHTTPHeader(client);

  // Le contenu de la r√©ponse HTTP suit l'en-t√™te :
  // il affiche l'√©tat de la LED et fournit des liens pour allumer ou √©teindre la LED.
  client.print("The LED is ");
  client.print(ledStatus);
  client.println("<br>");
  client.println("<br>");

  client.println("Click <a href=\"/H\">here</a> turn the LED on<br>");
  client.println("Click <a href=\"/L\">here</a> turn the LED off<br>");

  // The HTTP response ends with another blank line:
  client.println();
}

// Envoie l'ent√™te HTTP pour une r√©ponse
// Appeler celle-ci pour √©viter la duplication de code
void sendHTTPHeader(WiFiClient client) {
  // Les en-t√™tes HTTP commencent toujours par un code de r√©ponse (par exemple, HTTP/1.1 200 OK)
  // et un type de contenu pour que le client sache ce qui arrive, puis une ligne vide.
  client.println("HTTP/1.1 200 OK");
  client.println("Content-type:text/html");
  client.println();
}
```

---

# R√©sum√©
Dans cet article, on voit comment utiliser le module ESP8266 connect√© √† un Arduino Mega via un port s√©rie. On a vu comment configurer le module pour se connecter √† un r√©seau wifi et comment cr√©er un serveur web pour communiquer avec le module.

N'oubliez pas que la configuration pour connecter le module √† un r√©seau wifi doit √™tre faite une seule fois. Ensuite, il est possible de l'utiliser pour communiquer avec le module sans le reconfigurer.

---

# R√©f√©rences
- [Site officiel : ESP8266](https://www.espressif.com/en/products/socs/esp8266)
- [Documentation libraire WiFiEsp](https://github.com/bportaluri/WiFiEsp)
- [Web Server LED](https://github.com/bportaluri/WiFiEsp/blob/master/examples/WebServerLed/WebServerLed.ino)
- [Requ√™te HTTP](https://cshaw.jhoffman.ca/sessions/2022A/0SV/documentation/2.1-service-web/#requete-http-menu)