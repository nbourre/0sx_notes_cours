# La refactorisation <!-- omit in toc -->

# Table des mati√®res <!-- omit in toc -->
- [Introduction](#introduction)
- [Les principes de la refactorisation](#les-principes-de-la-refactorisation)
  - [Les principes SOLID](#les-principes-solid)
    - [Le principe de responsabilit√© unique (Single Responsibility Principle)](#le-principe-de-responsabilit√©-unique-single-responsibility-principle)
    - [Le principe d'ouverture/fermeture (Open/Closed Principle)](#le-principe-douverturefermeture-openclosed-principle)
- [√âtude de cas](#√©tude-de-cas)
  - [Identification des probl√®mes](#identification-des-probl√®mes)
  - [Analyse du projet](#analyse-du-projet)
  - [D√©terminer les responsabilit√©s de chaque classe](#d√©terminer-les-responsabilit√©s-de-chaque-classe)
    - [Classe `Eclairage`](#classe-eclairage)
    - [Classe `Affichage`](#classe-affichage)
  - [Code principal](#code-principal)
- [Exercices](#exercices)
  - [Syst√®me d'alarme](#syst√®me-dalarme)
    - [Requis](#requis)
  - [Affichage](#affichage)
    - [Requis](#requis-1)
- [R√©sum√©](#r√©sum√©)
- [Annexe](#annexe)
  - [Fonction membre en ligne](#fonction-membre-en-ligne)
  - [Initialisation des membres objets](#initialisation-des-membres-objets)


# Introduction
Voici une mise en situation.

Vous avez travaill√© sur un projet qui fonctionnait pour les besoins du moment. Mais maintenant, vous devez ajouter une nouvelle fonctionnalit√©. Vous vous rendez compte que le code est difficile √† comprendre et √† modifier. Si vous effectuez des modifications, vous risquez de casser le code existant. Vous d√©cidez donc de le r√©√©crire pour l'am√©liorer. C'est ce que l'on appelle la **refactorisation**.

La refactorisation est le processus de modification du code source pour am√©liorer sa structure interne sans changer son comportement externe. La refactorisation est g√©n√©ralement effectu√©e pour am√©liorer la lisibilit√© du code, faciliter sa maintenance et augmenter sa flexibilit√© pour les futures √©volutions du logiciel. Elle peut √©galement √™tre effectu√©e pour r√©duire la complexit√© du code ou pour √©liminer les d√©fauts de conception.

> **Note :** Il faudra comprendre les concepts des classes et des objets pour bien appr√©cier la refactorisation. Si ce n'est pas le cas, vous pouvez consulter le cours sur [les classes et les objets](../c08/c08b_poo_base/c08b_poo.md).

# Les principes de la refactorisation
La refactorisation est un processus qui peut √™tre appliqu√© √† n'importe quel code, mais il existe des principes qui peuvent vous aider √† am√©liorer votre code.

## Les principes SOLID
Les principes [SOLID](https://fr.wikipedia.org/wiki/SOLID_(informatique)) sont des principes de programmation orient√©e objet qui peuvent √™tre appliqu√©s √† n'importe quel langage de programmation. Ils sont souvent utilis√©s pour d√©crire les bonnes pratiques de la programmation orient√©e objet.

√Ä votre niveau, soit 1√®re ann√©e, nous allons voir 2 principes des 5 principes du SOLID.

### Le principe de responsabilit√© unique (Single Responsibility Principle)
Le principe de responsabilit√© unique (SRP) stipule qu'**une classe doit avoir une seule responsabilit√©**. Si une classe a plusieurs responsabilit√©s, cela signifie qu'elle est trop complexe et qu'elle doit √™tre divis√©e en plusieurs classes.

### Le principe d'ouverture/fermeture (Open/Closed Principle)
Le principe d'ouverture/fermeture (OCP) stipule qu'une classe doit √™tre ouverte √† l'extension, mais ferm√©e √† la modification. **Cela signifie que vous devez pouvoir ajouter de nouvelles fonctionnalit√©s sans modifier le code existant.**

# √âtude de cas
Pour pratiquer la refactorisation, nous allons refaire le laboratoire 04 soit celui de l'√©clairage automatique. Nous allons √©tudier le code qui suit. Il s'agit du code de votre coll√®gue Vincent Bureau (Merci! üôÇ).

```cpp
#include <LiquidCrystal.h>
#include <HCSR04.h>
const int rs = 36, en = 34, d4 = 32, d5 = 30, d6 = 28, d7 = 26;
LiquidCrystal lcd(rs, en, d4, d5, d6, d7);
const int contrastPin = 38;

HCSR04 hc(12, 11); 
int distance;

unsigned long currentMillis;
unsigned long previousMillis;
int displayDelay = 250;
int serialDelay = 100;
int ledPin = 4;
const int lumSensorPin = A0;
int lumValue;
int luminosity;
int lumMin = 1023;
int lumMax = 0;

void setup() {
  Serial.begin(9600);
  pinMode(ledPin, OUTPUT);
  analogWrite(contrastPin, 100);
  lcd.begin(16, 2); 
}

int ultraSon(int currentMillis){
  distance = hc.dist();
  return distance;
}

void display(int distance,unsigned long currentMillis,int luminosity){
  if (currentMillis - previousMillis >= displayDelay){
    previousMillis = currentMillis;
    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print(F("Lum : "));
    lcd.print(luminosity);
    lcd.print("%");
    lcd.setCursor(0,1);
    lcd.print(F("Dist : "));
    lcd.print(distance);
  }
}

int autoLum(int currentMillis, int distance){
  lumValue = analogRead(lumSensorPin);
  if (lumValue > lumMax ){
    lumMax = lumValue;
  }
  if (lumValue < lumMin){
    lumMin = lumValue;
  }
  luminosity = map(lumValue,lumMax,lumMin,0,100);

  if (luminosity < 30){
    digitalWrite(ledPin,HIGH);
  }else{
    digitalWrite(ledPin,LOW);
  }
  if(currentMillis-previousMillis >= serialDelay){
    currentMillis = previousMillis;
    Serial.print("Current:");
    Serial.println(luminosity);
    Serial.print("Min:");
    Serial.println(lumMin);
    Serial.print("Max:");
    Serial.println(lumMax);
    Serial.print("Distance:");
    Serial.println(distance);
  }
  return luminosity;
}
void loop() {
  currentMillis = millis();
  distance = ultraSon(currentMillis);
  luminosity = autoLum(currentMillis,distance);
  display(distance,currentMillis,luminosity);
}
```

> **Remarques :** Vous allez remarquer que j'identifie plusieurs probl√®mes dans le code et **c'est normal**. Vous √™tes en apprentissage et cela fait partie du processus. Au fur et √† mesure de votre formation, vous allez apprendre √† identifier les probl√®mes, √† les r√©soudre et √† les √©viter.

## Identification des probl√®mes

Avant de faire la refactorisation, il faut identifier les probl√©matiques du code actuel. 

Les questions que l'on peut se poser seraient "Est-ce que √ßa sert pour cette fonction?", "Est-ce que j'ai un √©quivalent ailleur?", "Est-ce qu'elle donne un avantage?", etc.

- La fonction `ultrason` :
  - est une fonction qui ne fait qu'appeler une fonction de la librairie `HCSR04`. Est-ce qu'elle donne un avantage?
  - a le param√®tre `currentMillis` qui n'est pas utilis√©. Est-ce que √ßa sert?
  
- La fonction `display` :
  - a le param√®tre `distance`. Est-ce que j'ai un √©quivalent ailleur?
  - a le param√®tre `currentMillis`. Est-ce que j'ai un √©quivalent ailleur?
  - a le param√®tre `luminosity`. Est-ce que j'ai un √©quivalent ailleur?
  - est d√©pendante d'√©l√©ment externe. Est-ce que tous les √©l√©ments qui sont dans la fonction servent √† celle-ci?
  
- La fonction `autoLum` :
  - a le param√®tre `currentMillis`. Est-ce que j'ai un √©quivalent ailleur?
  - a le param√®tre `distance`. Est-ce que j'ai un √©quivalent ailleur?
  - a des √©l√©ments qui n'ont pas de lien avec la fonction. Est-ce que tous les √©l√©ments qui sont dans la fonction servent √† celle-ci?
  - est d√©pendante d'√©l√©ment externe. Est-ce que tous les √©l√©ments qui sont dans la fonction servent √† celle-ci?

Ce sont tous des probl√©matiques que la refactorisation va permettre de r√©soudre.

## Analyse du projet
La premi√®re √©tape sera de d√©terminer les diff√©rents syst√®mes de ce projet.

Les grandes lignes du projet √©taient ceci :
- Lire les valeurs du capteur de luminosit√©;
- Calibrer le d√©clenchement de la lumi√®re avec la valeur minimum et maximum du capteur de luminosit√©;
- Allumer une DEL lorsque la luminosit√© est plus basse qu'un seuil;
- Lire la valeur de la distance avec le capteur ultrason;
- Afficher des valeurs dans le port s√©rie
- Afficher des valeurs sur l'√©cran LCD.

Dans ce projet, nous avons 3 syst√®mes :
- Le syst√®me de lecture de la luminosit√©;
- Le syst√®me de lecture de la distance;
- Le syst√®me d'affichage.

Le syst√®me de distance n'a aucun lien avec le syst√®me de luminosit√©. Il n'y a donc pas de raison de les regrouper. Nous avons donc besoin 2 classes pour ces syst√®mes.

L'affichage est un syst√®me qui a besoin des donn√©es des autres syst√®mes. Il re√ßoit donc les donn√©es des autres syst√®mes. On effectue aussi plusieurs op√©rations avec celui-ci. Nous avons donc besoin d'une classe pour l'affichage. Nous avons donc besoin de 3 classes pour ce projet.

Pour le syst√®me de distance, nous n'avons pas besoin de faire de classe, car on utilise d√©j√† la librairie `HCSR04` et on ne fait que lire la distance.

> **Note :** Je sais que nous avons le syst√®me d'alarme, mais on se concentre sur le syst√®me d'√©clairage automatique.

## D√©terminer les responsabilit√©s de chaque classe
Nous avons donc besoin de 2 classes pour ce projet. Nous allons maintenant d√©terminer les responsabilit√©s de chaque classe.

Pour l'instant, on n'a pas besoin de classe pour la distance. Nous allons donc nous concentrer sur les 2 autres classes.

### Classe `Eclairage`
La classe `Eclairage` va g√©rer le syst√®me d'√©clairage. Elle va lire la valeur du capteur de luminosit√© et allumer la DEL lorsque la luminosit√© est trop basse.

Dans sa premi√®re moutures, les fonctions publiques de cette classe seront :
- `getLuminosity()`; Pour lire la valeur du capteur de luminosit√©;
- `update()`; Pour mettre √† jour la valeur de la luminosit√© en continu;
- `getMinLuminosity()`; Qui retournera la valeur minimum de la luminosit√©;
- `getMaxLuminosity()`; Qui retournera la valeur maximum de la luminosit√©;
- `setThreshold(int threshold)`; Pour d√©finir le seuil de d√©clenchement de la DEL;
- `getThreshold()`; Pour obtenir le seuil de d√©clenchement de la DEL;

Voici un code qui r√©pond √† l'analyse de la classe `Eclairage` :

```cpp
class Eclairage{
  public:
    // Constructeur
    Eclairage(int lumSensorPin, int ledPin);

    // Fonction qui retourne la valeur du capteur de luminosit√©
    int getLuminosity() { return _lumValue; }

    // Fonction qui retourne la valeur minimum du capteur de luminosit√©
    int getMinLuminosity() { return _lumMin; }

    // Fonction qui retourne la valeur maximum du capteur de luminosit√©
    int getMaxLuminosity() { return _lumMax; }

    // Fonction qui d√©fini le seuil de d√©clenchement de la DEL
    void setThreshold(int threshold);

    // Fonction qui retourne le seuil de d√©clenchement de la DEL
    int getThreshold() { return _threshold; }

    // Fonction appel√©e dans le loop
    void update();

  private:
    int _lumSensorPin;
    int _ledPin;
    int _lumValue;
    int _lumMin;
    int _lumMax;
    int _threshold;
};

// Peut √™tre d√©plac√© dans le fichier Eclairage.cpp
Eclairage::Eclairage(int lumSensorPin, int ledPin){
  _lumSensorPin = lumSensorPin;
  _ledPin = ledPin;
  _lumMin = 1023;
  _lumMax = 0;
  _threshold = 30;
  pinMode(_ledPin, OUTPUT);
}

void Eclairage::update(){
  _lumValue = analogRead(_lumSensorPin);
  if (_lumValue > _lumMax ){
    _lumMax = _lumValue;
  }
  if (_lumValue < _lumMin){
    _lumMin = _lumValue;
  }
  int luminosity = map(_lumValue,_lumMin,_lumMax,0,100);
  if (luminosity < _threshold){
    digitalWrite(_ledPin,HIGH);
  }else{
    digitalWrite(_ledPin,LOW);
  }
}
```

### Classe `Affichage`
Pour la classe `Affichage`, celle-ci va recevoir les donn√©es des autres classes et les afficher sur l'√©cran LCD. Ne sachant pas encore tous les syst√®mes qui devront afficher de l'information, nous allons d√©velopper une classe ind√©pendante de tout autre syst√®me.

On veut rafra√Æchir l'affichage √† toutes les temps donn√©es. On a donc besoin d'une fonction pour indiquer le temps entre chaque rafra√Æchissement.

Dans sa premi√®re moutures, les fonctions publiques de cette classe seront :
- `setLine1(String line1)`; Pour d√©finir la premi√®re ligne de l'√©cran LCD;
- `setLine2(String line2)`; Pour d√©finir la deuxi√®me ligne de l'√©cran LCD;
- `update()`; Pour mettre √† jour l'affichage sur l'√©cran LCD;
- `clear()`; Pour effacer l'√©cran LCD;
- `setRefreshRate(int refreshRate)`; Pour d√©finir le temps entre chaque rafra√Æchissement de l'√©cran LCD;
- `getRefreshRate()`; Pour obtenir le temps entre chaque rafra√Æchissement de l'√©cran LCD;

Voici le code qui r√©pond √† l'analyse de la classe `Affichage` :

```cpp
#include <LiquidCrystal_I2C.h>

class Affichage {
  public:
    // Constructeur
    Affichage(uint8_t lcdAddress, uint8_t lcdColumns, uint8_t lcdRows);

    void setLine1(String line1);
    void setLine2(String line2);
    void update();
    void clear();
    void setRefreshRate(int refreshRate) { _refreshRate = refreshRate; }
    int getRefreshRate() { return _refreshRate; }

  private:
    int _refreshRate = 250;
    String _line1;
    String _line2;
    
    unsigned long _currentTime;
    bool _needUpdate = false; // Drapeau pour indiquer si l'affichage doit √™tre mis √† jour
    
    LiquidCrystal_I2C _lcd;
};

// Peut √™tre d√©plac√© dans le fichier Affichage.cpp
Affichage::Affichage(uint8_t lcdAddress, uint8_t lcdColumns, uint8_t lcdRows):
  _lcd (lcdAddress, lcdColumns, lcdRows) // Initialisation de l'√©cran LCD. Voir note plus bas.
{
  _lcd.init();
  _lcd.backlight();
}

void Affichage::setLine1(String line1){
  // On sort de la m√©thode si la ligne est la m√™me que la pr√©c√©dente 
  if (line1.equals(_line1)){
    return;
  }

  _line1 = line1;
  _needUpdate = true;
}

void Affichage::setLine2(String line2){
  // On sort de la m√©thode si la ligne est la m√™me que la pr√©c√©dente
  if (line2.equals(_line2)){
    return;
  }
  _line2 = line2;
  _needUpdate = true;
}

void Affichage::clear(){
  _lcd.clear();
}

void Affichage::update(){
  static unsigned long lastUpdate = 0;
  _currentTime = millis();

  // On sort de la m√©thode si le temps entre le dernier rafra√Æchissement est plus petit que le temps de rafra√Æchissement
  if (_currentTime - lastUpdate < _refreshRate){
    return;
  }

  // Pourquoi afficher si on n'a pas besoin de mettre √† jour l'affichage?
  if (!_needUpdate){
    return;
  }
  
  lastUpdate = _currentTime;

  _lcd.clear();
  _lcd.setCursor(0,0);
  _lcd.print(_line1);
  _lcd.setCursor(0,1);
  _lcd.print(_line2);

  // On redescent le drapeau
  _needUpdate = false;
}
```

> **Note :** Certaines classes n'ont pas de constructeur par d√©faut, il faut donc les initialiser dans le constructeur de la classe. Dans le cas de l'√©cran LCD, selon la librairie, il faut obligatoirement lui passer l'adresse, le nombre de colonnes et le nombre de lignes. C'est pourquoi on a besoin de passer ces param√®tres au constructeur de la classe `Affichage`.
> 
> La syntaxe g√©n√©rale pour initialiser les membres objets est la suivante :
> ```cpp
> NomClasse::NomClasse(param√®tres):
>  _nomMembreObjet(param√®tres) // Initialisation du membre objet
>  , _nomMembreObjet2(param√®tres) // Initialisation du membre objet 2
>  //,... // Ainsi de suite. N'oubliez pas la virgule
> {
>   // Code du constructeur
> }
> ```


## Code principal
Une fois que les classes sont r√©alis√©es, il ne reste plus qu'√† les utiliser dans le code principal. 

```cpp
#include <Arduino.h>
#include "Eclairage.h"
#include "Affichage.h"
#include "HCSR04.h"

#define LED_PIN 4
#define LUM_SENSOR_PIN A0
#define ECHO_PIN 2
#define TRIGGER_PIN 3
#define LCD_ADDRESS 0x27
#define LCD_COLUMNS 16
#define LCD_ROWS 2

Eclairage eclairage(LUM_SENSOR_PIN, LED_PIN);
HCSR04 hc(ECHO_PIN, TRIGGER_PIN);
Affichage affichage(LCD_ADDRESS, LCD_COLUMNS, LCD_ROWS);

unsigned long currentTime;

void setup() {
  Serial.begin(9600);  
}

void loop() {
  currentTime = millis();

  eclairage.update();

  affichage.setLine1("Distance: " + String(hc.getDistance()));
  affichage.setLine2("Luminosite: " + String(eclairage.getLuminosity()));
  affichage.update();
}

```

Voil√†! Le code principal est maintenant beaucoup plus lisible et facile √† comprendre.

---

# Exercices
## Syst√®me d'alarme
Refactorisez le code du laboratoire du syst√®me d'alarme.

### Requis
- La classe devra se nommer `Alarme`.
- Le constructeur devra avoir les broches pour le d√©tecteur de distance, les broches des DEL et le broche du buzzer.
  - La signature sera donc `Alarme(uint8_t echoPin, uint8_t triggerPin, uint8_t ledRed, uint8_t ledBlue, uint8_t buzzerPin);`
- Il devra y avoir la fonction en ligne `setTriggerDistance(uint16_t triggerDistance)` qui permettra de d√©finir la distance de d√©clenchement de l'alarme.
- Il devra y avoir la fonction en ligne `getTriggerDistance()` qui retournera la distance de d√©clenchement de l'alarme.
- Il devra y avoir la fonction en ligne `getDistance()` qui retournera la distance du d√©tecteur de distance.
- Il devra y avoir les √©tats "ON" et "OFF" pour l'alarme.
  - Dans l'√©tat "ON", si la distance est sup√©rieure √† la distance de d√©clenchement pendant plus de 3 secondes, l'alarme s'arr√™te.
- Il devra y avoir la fonction `update()` qui mettra √† jour l'√©tat de l'alarme.
  - On devra mettre √† jour la distance √† tous les 10 ms.

## Affichage
Le code du LCD est assez p√™le-m√™le. Refactorisez le code pour qu'il soit plus lisible.

### Requis
Il n'est pas n√©cessaire de cr√©er une classe pour cet exercice. Il suffit de cr√©er des √©l√©ments qui permettront de rendre le code plus lisible.

Ajoutez un √©num√©ration nomm√©e `LCDState` pour l'√©tat de l'affichage. Les √©tats serviront √† indiquer quels messages √† afficher. Ils seront les suivants :
- `ECLAIRAGE` : Affiche la luminosit√©
- `ALARME` : Affiche la distance
- `GARAGE` : Affiche l'√©tat de la porte de garage
- `AC` : Affiche l'√©tat de la climatisation
- `DATA_RECEIVED` : Affiche que les donn√©es ont √©t√© re√ßues

Ajoutez une fonction `lcdTask()` qui sera appel√©e dans la fonction `loop()`. Cette fonction devra avoir le contenu suivant :

```cpp
void lcdTask() {
  static unsigned long lastUpdate = 0;
  static unsigned long lastMsgChange = 0;

  const int lcdRefreshRate = 250; // Temps entre chaque rafra√Æchissement de l'affichage
  const int nextStateRate = 3000; // Temps entre chaque changement d'√©tat

  if (currentTime - lastUpdate < lcdRefreshRate){
    return; // On sort si le temps n'est pas √©coul√©
  }

  lastUpdate = currentTime;

  switch (lcdState) {
    case ECLAIRAGE:
      lcdEclairage();
      // On passe au prochain √©tat
      if (changeState) {
        lcdState = ALARME;
        changeState = false;
      }
      break;
    case ALARME:
      lcdAlarme();
      // On passe au prochain √©tat
      if (changeState) {
        lcdState = GARAGE;
        changeState = false;
      }
      break;
    case GARAGE:
      lcdGarage();
      // TODO
      break;
    case AC:
      lcdAC();
      // TODO
      break;
    case DATA_RECEIVED:
      lcdDataReceived();
      // TODO
      break;
    default:
      break;
  }

  if (currentTime - lastMsgChange > nextStateRate) {
    changeState = true;
    lastMsgChange = currentTime;
  }
}

void lcdEclairage() {
  static int luminosity = 0;
  static int threshold = 0;

  luminosity = eclairage.getLuminosity();
  threshold = eclairage.getThreshold();

  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Luminosite: ");
  lcd.print(luminosity);
  lcd.print("%");
  lcd.setCursor(0, 1);
  lcd.print("Etat: ");
  if (luminosity < threshold) {
    lcd.print("Allume");
  } else {
    lcd.print("Eteint");
  }
}

void lcdAlarme() {
  // Ligne 1 : Afficher la distance
  // Ligne 2 : Afficher l'√©tat de l'alarme
}

void lcdGarage() {
  // Ligne 1 : Afficher l'√©tat de la porte de garage
}

void lcdAC() {
  // Ligne 1 : Afficher l'√©tat de la climatisation
}

void lcdDataReceived() {
  // Ligne 1 : Afficher "To be defined"
}
```

---

# R√©sum√©
Dans ce tutoriel, nous avons vu comment utiliser les classes pour organiser le code. Nous avons vu comment d√©finir une classe et comment utiliser les attributs et les m√©thodes d'une classe. Nous avons aussi vu comment utiliser les classes dans le code principal.

Il faut se rappeler que si j'utilise un attribut qui est un objet, il faudra faire une r√©f√©rence √† cet objet √† l'aide du symbole `&` dans le constructeur de la classe.

---

# Annexe
## Fonction membre en ligne
Dans le code de la classe `Affichage`, nous avons utilis√© une fonction membre en ligne. C'est une fonction qui est d√©finie dans la classe, mais qui n'est pas d√©finie dans le fichier `.cpp`. Cette fonction est d√©finie dans le fichier `.h` et est donc accessible partout o√π la classe est incluse.

Elle poss√®de g√©n√©ralement peu de lignes de code et est donc plus facile √† lire et √† comprendre.

Voici la syntaxe g√©n√©rale pour d√©finir une fonction membre en ligne :

```cpp
class NomClasse {
  public:
    // D√©claration de la fonction membre en ligne
    void nomFonction(param√®tres) { code de la fonction }
};
```

Voici un exemple de fonction membre en ligne :

```cpp
class Affichage {
  public:
    void setRefreshRate(int refreshRate) { _refreshRate = refreshRate; }
};
```

## Initialisation des membres objets
Dans le code de la classe `Affichage`, nous avons initialis√© les membres objets dans le constructeur de la classe. C'est une bonne pratique de faire cela, car cela permet de d√©finir les valeurs par d√©faut des membres objets. De plus, certains membres objets n'ont pas de constructeur par d√©faut, il faut donc les initialiser dans le constructeur de la classe.

Voici la syntaxe g√©n√©rale pour initialiser les membres objets :

```cpp
class NomClasse {
  public:
    NomClasse(param√®tres): // Ne pas oublier les deux points
      _nomMembreObjet(param√®tres) // Initialisation du membre objet
      , _nomMembreObjet2(param√®tres) // Initialisation du membre objet 2
      //,... // Ainsi de suite. N'oubliez pas la virgule
    {
      // Code du constructeur
    }
};
```

Voici un exemple d'initialisation des membres objets :

```cpp
class Affichage {
  public:
    Affichage(uint8_t lcdAddress, uint8_t lcdColumns, uint8_t lcdRows):
      _lcd (lcdAddress, lcdColumns, lcdRows) // Initialisation de l'√©cran LCD. Voir note plus bas.
    {
      _lcd.init();
      _lcd.backlight();
    }
  private:
    LiquidCrystal_I2C _lcd; // D√©claration de l'√©cran LCD
};
```

Prenez note qu'il faut les membres objets soient d√©clar√©s avant l'initialisation dans le constructeur.